if (target_os == "") {
  target_os = host_os
}
if (target_cpu == "") {
  target_cpu = host_cpu
}
if (current_os == "") {
  current_os = target_os
}
if (current_cpu == "") {
  current_cpu = target_cpu
}

is_linux = current_os == "linux"
is_macos = current_os == "mac"
is_windows = current_os == "win"

declare_args() {
  is_debug = true
}

_default_toolchain = ""
if (target_os == "mac") {
  assert(host_os == "mac")
  _default_toolchain = "//gn/toolchain:macos_clang"
} else if (target_os == "win") {
  assert(host_os == "win")
  _default_toolchain = "//gn/toolchain:windows_msvc"
} else {
  assert(false)
}
set_default_toolchain(_default_toolchain)

_default_compiler_configs = [
  "//gn/config:basics",
  "//gn/config:default_standard",
  "//gn/config:default_optimization",
  "//gn/config:default_includes",
  "//gn/config:warnings",
  "//gn/config:no_rtti",
  "//gn/config:no_exceptions",
  "//gn/config:objc_arc",
  "//gn/config:local_overrides",
]

set_defaults("static_library") {
  configs = _default_compiler_configs
}
set_defaults("shared_library") {
  configs = _default_compiler_configs
}
set_defaults("source_set") {
  configs = _default_compiler_configs
}

_default_linker_configs = []
_default_executable_configs = _default_compiler_configs + _default_linker_configs

set_defaults("executable") {
  configs = _default_executable_configs
}
