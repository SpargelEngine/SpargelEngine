import("//gn/config/vulkan.gni")
import("//gn/autoconfig.gni")

autoconfig("build_config") {
    header = "build_config.h"

    enable_metal = is_macos

    flags = [
        "SPARGEL_GPU_ENABLE_VULKAN=$enable_vulkan",
        "SPARGEL_GPU_ENABLE_METAL=$enable_metal",
    ]
}

source_set("gpu") {
    sources = [
        "gpu_context.cpp",
        "gpu.cpp",
    ]
    public = [
        "gpu_context.h",
        "gpu.h",
        "tlsf_allocator.h"
    ]
    deps = [
        ":build_config",
        "//source/spargel/base",
        "//source/spargel/codec",
        "//source/spargel/logging",
    ]

    if (is_macos) {
        sources += [
            "gpu_metal.mm",
            "metal_context.mm",
            "metal_shader_manager.cpp",
        ]
        frameworks = [
            "Foundation.framework",
            "Metal.framework",
        ]
    }

    if (enable_vulkan) {
        sources += [
            "gpu_vulkan.cpp",
        ]
        include_dirs = [
            "$vulkan_sdk/include",
        ]
    }
}

executable("demo_gpu") {
    sources = [ "demo_gpu.cpp" ]
    deps = [
        ":gpu",
        "//source/spargel/codec",
        "//source/spargel/json",
    ]
}

executable("gpu_tests") {
    sources = [ "tlsf_allocator_tests.cpp" ]
    deps = [
        ":gpu",
        "//source/spargel/base",
        "//source/spargel/base:test_main",
    ]
}
