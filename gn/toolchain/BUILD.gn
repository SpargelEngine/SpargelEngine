import("//gn/config/clang.gni")
import("//gn/config/msvc.gni")

toolchain("macos_clang") {
    prefix = rebase_path("$clang_root/bin/", root_build_dir)
    cc = "${prefix}clang"
    cxx = "${prefix}clang++"
    ld = cxx
    ar = "${prefix}llvm-ar"

    object_subdir = "{{target_out_dir}}/{{label_name}}"

    tool("cc") {
        depfile = "{{output}}.d"
        depsformat = "gcc"
        outputs = [
            "$object_subdir/{{source_name_part}}.o"
        ]
        command = "$cc -MMD -MF $depfile {{defines}} {{include_dirs}} {{cflags}} {{cflags_c}} -c {{source}} -o {{output}}"
        description = "CC {{output}}"
    }
    tool("cxx") {
        depfile = "{{output}}.d"
        depsformat = "gcc"
        outputs = [
            "$object_subdir/{{source_name_part}}.o"
        ]
        command = "$cxx -MMD -MF $depfile {{defines}} {{include_dirs}} {{cflags}} {{cflags_cc}} -c {{source}} -o {{output}}"
        description = "CXX {{output}}"
    }
    tool("objc") {
        depfile = "{{output}}.d"
        depsformat = "gcc"
        outputs = [
            "$object_subdir/{{source_name_part}}.o"
        ]
        command = "$cc -MMD -MF $depfile {{defines}} {{include_dirs}} {{framework_dirs}} {{cflags}} {{cflags_objc}} -c {{source}} -o {{output}}"
        description = "OBJC {{output}}"
    }
    tool("objcxx") {
        depfile = "{{output}}.d"
        depsformat = "gcc"
        outputs = [
            "$object_subdir/{{source_name_part}}.o"
        ]
        command = "$cxx -MMD -MF $depfile {{defines}} {{include_dirs}} {{framework_dirs}} {{cflags}} {{cflags_objcc}} -c {{source}} -o {{output}}"
        description = "OBJCXX {{output}}"
    }
    tool("alink") {
        rspfile = "{{output}}.rsp"
        rspfile_content = "{{inputs}}"
        default_output_dir = "{{target_out_dir}}"
        default_output_extension = ".a"
        outputs = [
            "{{output_dir}}/{{target_output_name}}{{output_extension}}",
        ]
        command = "rm -f {{output}} && $ar -r -c -s -D {{arflags}} {{output}} @$rspfile"
        description = "AR {{output}}"
    }
    tool("solink") {
        outfile = "{{output_dir}}/{{target_output_name}}{{output_extension}}"
        rspfile = "$outfile.rsp"
        rspfile_content = "{{inputs}} {{frameworks}} {{swiftmodules}} {{solibs}} {{libs}}"
        default_output_dir = "{{root_out_dir}}"
        default_output_extension = ".dylib"
        outputs = [ outfile ]
        command = "$ld -shared {{ldflags}} -o $outfile @$rspfile"
        description = "SOLINK $outfile"
    }
    tool("link") {
        outfile = "{{output_dir}}/{{target_output_name}}{{output_extension}}"
        rspfile = "$outfile.rsp"
        # rspfile_content = "-Wl,--start-group {{inputs}} -Wl,--end-group {{frameworks}} {{swiftmodules}} {{solibs}} {{libs}}"
        rspfile_content = "{{inputs}} {{frameworks}} {{swiftmodules}} {{solibs}} {{libs}}"
        default_output_dir = "{{root_out_dir}}"
        outputs = [ outfile ]
        command = "$ld {{ldflags}} -o $outfile @$rspfile"
        description = "LINK $outfile"
    }
}

toolchain("windows_msvc") {
    prefix = rebase_path("$msvc_bin_path\\", root_build_dir)
    cl = "${prefix}cl"
    link = "${prefix}link"

    object_subdir = "{{target_out_dir}}/{{label_name}}"

    lib_switch = ""
    lib_dir_switch = "/LIBPATH:"

    tool("cc") {
        depsformat = "msvc"
        outputs = [
            "$object_subdir/{{source_name_part}}.o"
        ]
        command = "$cl /nologo /showIncludes {{defines}} {{include_dirs}} {{cflags}} {{cflags_c}} /c {{source}} /Fo{{output}}"
        description = "CC {{output}}"
    }
    tool("cxx") {
        depsformat = "msvc"
        outputs = [
            "$object_subdir/{{source_name_part}}.o"
        ]
        command = "$cl /nologo /showIncludes {{defines}} {{include_dirs}} {{cflags}} {{cflags_cc}} /c {{source}} /Fo{{output}}"
        description = "CXX {{output}}"
    }
    tool("link") {
        outfile = "{{output_dir}}/{{target_output_name}}{{output_extension}}"
        pdbfile = "{{output_dir}}/{{target_output_name}}.pdb"
        default_output_dir = "{{root_out_dir}}"
        default_output_extension = ".exe"
        outputs = [ outfile ]
        command = "$link /nologo {{ldflags}} /out:$outfile /pdb:$pdbfile {{inputs}} {{libs}}"
    }
}