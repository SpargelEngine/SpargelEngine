import("//gn/config/sysroot.gni")
import("//gn/config/local.gni")

config("basics") {
  cflags = ["-fcolor-diagnostics"]
  ldflags = [
    "-fuse-ld=lld",
    "-Wl,--color-diagnostics",
  ]

  # TODO: split to sysroot config
  if (is_macos) {
    sysroot_flags = [
      "-isysroot",
      rebase_path(sysroot, root_build_dir),
    ]
    cflags += sysroot_flags
    ldflags += sysroot_flags
  }
}

config("default_standard") {
  cflags_c = ["-std=c11"]
  cflags_cc = ["-std=c++20"]
  cflags_objcc = ["-std=c++20"]
}

config("default_includes") {
  include_dirs = [
    "//",
    "$root_gen_dir",
  ]
}

# these are warnings enabled for spargel code
config("warnings") {
  cflags = [
    "-pedantic",
    "-pedantic-errors",
    "-Wall",
    "-Wextra",
    "-Wconversion",
    "-Werror",
  ]
}

config("default_optimization") {
  cflags = []
  if (!is_debug) {
    cflags += ["-O2"]
  } else {
    cflags += ["-O0"]
  }
}

# language features ------------------------------------------------------------

config("no_rtti") {
  cflags_cc = ["-fno-rtti"]
}

config("no_exceptions") {
  cflags_cc = ["-fno-exceptions"]
}

config("objc_arc") {
  cflags_objc = ["-fobjc-arc"]
  cflags_objcc = ["-fobjc-arc"]
}

# user specific config ---------------------------------------------------------

config("local_overrides") {
    ldflags = local_override_ldflags
}
