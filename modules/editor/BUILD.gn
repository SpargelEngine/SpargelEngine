executable("editor") {
  sources = [
    "private/main.cc",
  ]
  deps = [ "//modules/ui" ]
}

if (is_macos) {
  bundle_data("editor_bundle_info_plist") {
    sources = [ "Info.plist" ]
    outputs = [ "{{bundle_contents_dir}}/Info.plist" ]
  }
  bundle_data("editor_bundle_executable") {
    sources = [ "$root_out_dir/editor" ]
    outputs = [ "{{bundle_executable_dir}}/editor" ]
    public_deps = [ ":editor" ]
  }

  create_bundle("SpargelEditor.app") {
    bundle_root_dir = "$root_build_dir/$target_name"
    bundle_contents_dir  = "$bundle_root_dir/Contents"
    bundle_resources_dir = "$bundle_contents_dir/Resources"
    bundle_executable_dir = "$bundle_contents_dir/MacOS"

    deps = [
      ":editor_bundle_info_plist",
      ":editor_bundle_executable",
    ]
  }
}
