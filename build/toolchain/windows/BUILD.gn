import("//build/config/windows.gni")
toolchain("msvc_x64") {
  prefix = msvc_bin_path

  cl = "${prefix}cl.exe"
  link = "${prefix}link.exe"
  lib = "${prefix}lib.exe"

  object_subdir = "{{target_out_dir}}/{{label_name}}"

  tool("cc") {
    depfile = "{{output}}.d"
    depsformat = "msvc"
    outputs = [
      "$object_subdir/{{source_name_part}}.obj"
    ]
    pdbname = "{{target_out_dir}}/{{label_name}}_c.pdb"
    command = "$cl /nologo /showIncludes {{defines}} {{include_dirs}} {{cflags}} {{cflags_c}} /c {{source}} /Fo{{output}} /Fd$pdbname"
    description = "CC {{output}}"
  }
  tool("cxx") {
    depfile = "{{output}}.d"
    depsformat = "msvc"
    outputs = [
      "$object_subdir/{{source_name_part}}.obj"
    ]
    pdbname = "{{target_out_dir}}/{{label_name}}_cc.pdb"
    command = "$cl /nologo /showIncludes {{defines}} {{include_dirs}} {{cflags}} {{cflags_cc}} /c {{source}} /Fo{{output}} /Fd$pdbname"
    description = "CXX {{output}}"
  }
  tool("alink") {
    rspfile = "{{output}}.rsp"
    rspfile_content = "{{inputs_newline}}"
    default_output_dir = "{{target_out_dir}}"
    default_output_extension = ".lib"
    outputs = [
      "{{output_dir}}/{{target_output_name}}{{output_extension}}",
    ]
    command = "$lib /nologo /OUT:{{output}} {{arflags}} @$rspfile"
    description = "LIB {{output}}"
  }
  lib_switch = ""
  tool("link") {
    outfile = "{{output_dir}}/{{target_output_name}}{{output_extension}}"
    rspfile = "$outfile.rsp"
    pdbfile = "$outfile.pdb"
    rspfile_content = "{{inputs_newline}} {{libs}} {{solibs}} {{ldflags}}"
    default_output_dir = "{{root_out_dir}}"
    default_output_extension = ".exe"
    outputs = [ outfile, pdbfile ]
    command = "$link /nologo /out:$outfile /pdb:$pdbfile @$rspfile"
    description = "LINK $outfile"
  }
}
